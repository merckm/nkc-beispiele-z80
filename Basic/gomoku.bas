5 DIM A(100), X(4)
10 REM *********************************
11 REM * GOMOKU                        *
12 REM * ------                        *
13 REM * Version von Tim Hartnell      *
14 REM * Giant Book of Computer Games  *
15 REM * Angepasst an NKC Martin Merck *
16 REM *********************************
20 GOSUB 750
30 GOSUB 130
40 GOSUB 240
50 GOSUB 130
60 GOSUB 320
70 GOSUB 130
80 IF L > 3 THEN PRINT: PRINT "I WIN!!": END
90 GOTO 40
100 E = A
110 E = E + N: IF A(E) <> Z THEN RETURN
120 K = K + 1: GOTO 110
130 CLRS
140 PRINT: PRINT: PRINT
150 PRINT TAB(12); "1 2 3 4 5 6 7 8"
160 FOR A = 1 TO 8: PRINT TAB(8); A; " ";
170 FOR B = 2 TO 9
180 PRINT CHR$(A(A * 10 + B)); " ";
190 NEXT B
200 PRINT A
210 NEXT A
220 PRINT TAB(12); "1 2 3 4 5 6 7 8"
230 RETURN
240 PRINT: PRINT
250 PRINT "Please enter your move..."
260 INPUT G
270 G = G + 1
280 IF G < 12 OR G > 89 OR A(G) <> 46 THEN 260
290 Z = H
300 A(G) = Z
310 RETURN
320 A = G
330 L = 0
340 FOR X = 1 TO 4: K = 0: N = X(X)
350 GOSUB 100
360 N = -N: GOSUB 100
370 IF K > L THEN L = K
380 NEXT X
390 IF L > 3 THEN PRINT: PRINT "You win!!": END
400 T = 1
410 IF T <> 2 THEN Z = C
420 IF T = 2 THEN Z = H
430 G = 0: H1 = 0: L = 0
440 FOR A = 12 TO 89
450 M = 0
460 IF A(A) <> 46 THEN 570
470 FOR X = 1 TO 4: K = 0: N = X(X)
480 GOSUB 100
490 N = -N: GOSUB 100
500 IF K > L THEN H1 = 0: L = K
510 IF L <> K THEN 540
520 IF T = 1 AND L < 4 OR (T = 2 OR T = 3) AND L < 2 THEN 540
530 M = M + 1
540 NEXT X
550 IF M <= H1 THEN 570
560 H1 = M: G = A
570 NEXT A
580 IF H1 <> 0 THEN 650
590 T = T + 1: IF T <> 4 THEN 410
600 A = 1
610 G = INT(RND(0) * 77) + 13
620 IF A(G) = 46 THEN 650
630 A = A + 1: IF A < 100 THEN 610
640 PRINT: PRINT "I concede the game": PRINT "to a master!!": END
650 A(G) = C
660 Z = C: A = G: L = 0
670 FOR X = 1 TO 4
680 K = 0
690 N = X(X)
700 GOSUB 100
710 N = -N: GOSUB 100
720 IF K > L THEN L = K
730 NEXT X
740 RETURN
750 CLRS
770 FOR C = 1 TO 8
780 FOR B = 2 TO 9
790 A(C * 10 + B) = 46
800 NEXT B
810 NEXT C
820 FOR Q = 1 TO 4
830 READ Z: X(Q) = Z
840 NEXT Q
850 DATA 1,9,10,11
860 H = ASC("H"): C = ASC("C")
870 PRINT: PRINT "Enter Y if you want the"
880 INPUT "first move, N if you don't";A$
890 N = 0
900 N = N + 1
920 IF A$ <> "y" AND A$ <> "Y" AND A$ <> "n" AND A$ <> "N" THEN CLRS: GOTO 870
930 REM RANDOMIZE N
940 CLRS
950 IF A$ = "y" OR A$ = "Y" THEN RETURN
960 FOR J = 1 TO INT(RND(0) * 12) + 1
970 READ Z
980 NEXT J
990 A(Z) = C
1000 RETURN
1010 DATA 34,35,44,46,47,54,55,56,57,66