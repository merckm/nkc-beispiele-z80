1 REM PROGRAMM ZUR AUSGABE EINES TEST-HISTOGRAMMS
2 REM AUTHOR: Martun Merck
10 CLRS:CLEAR 100
20 DIM A(8)
30 DATA 123.475,46.234,136.7,56.789,99.999,111.111,12.345,87.654
40 FOR I=1 TO 8
50 READ A(I)
60 NEXT I
70 GOSUB 200
80 PRINT "WERTE";TAB(12);"HISTOGRAMM"
90 PRINT
100 FOR I=1 TO 8
110 PRINT A(I);TAB(12);
120 FOR J=1 TO INT(A(I)*40./XMAX)
130 PRINT "*";
140 NEXT J
150 PRINT
160 NEXT I
170 END
200 REM Unterprogram zur bestimmung des Maximums
210 XMAX = 0
220 FOR K=1 TO 8
230 IF A(K) <= XMAX THEN 250
240 XMAX = A(K)
250 NEXT K
260 RETURN